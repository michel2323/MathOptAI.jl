# Copyright (c) 2024: Triad National Security, LLC
# Copyright (c) 2024: Oscar Dowson and contributors
#
# Use of this source code is governed by a BSD-style license that can be found
# in the LICENSE.md file.

"""
     GrayBox(
        predictor::P;
        device::String = "cpu",
        hessian::Bool = true,
    ) where {P}

An [`AbstractPredictor`](@ref) that represents the relationship:
```math
y = f(x)
```
as a vector nonlinear operator.

This predictor should not be used directly; it is intended to be used by
extensions like Flux and PyTorch.

## Example

```jldoctest
julia> using JuMP, MathOptAI, Flux

julia> chain = Flux.Chain(Flux.Dense(1 => 16, Flux.relu), Flux.Dense(16 => 1));

julia> model = Model();

julia> @variable(model, x[1:1]);

julia> y, _ = MathOptAI.add_predictor(model, chain, x; gray_box = true);

julia> y
1-element Vector{VariableRef}:
 moai_GrayBox[1]

julia> print(model)
Feasibility
Subject to
 [x[1], moai_GrayBox[1]] ∈ VectorNonlinearOracle{Float64}(;
     dimension = 2,
     l = [0.0],
     u = [0.0],
     ...,
 )
```
"""
struct GrayBox{P} <: AbstractPredictor
    predictor::P
    device::String
    hessian::Bool

    function GrayBox(
        predictor::P;
        device::String = "cpu",
        hessian::Bool = true,
    ) where {P}
        return new{P}(predictor, device, hessian)
    end
end

"""
    graybox_output_dim(predictor, n_input::Int) -> Int

Return the number of outputs for a gray-box predictor given `n_input` inputs.
Must be implemented by concrete predictor types used with [`GrayBox`](@ref).
"""
function graybox_output_dim end

"""
    graybox_eval!(y, predictor, x)

Evaluate the gray-box predictor: `y .= f(x)`.
Must be implemented by concrete predictor types used with [`GrayBox`](@ref).
"""
function graybox_eval! end

"""
    graybox_vjp!(Jtv, predictor, x, w)

Compute the vector-Jacobian product: `Jtv .= J(x)' * w`.
Must be implemented by concrete predictor types used with [`GrayBox`](@ref).
"""
function graybox_vjp! end

"""
    graybox_jvp!(Jv, predictor, x, v)

Compute the Jacobian-vector product: `Jv .= J(x) * v`.
Must be implemented by concrete predictor types used with [`GrayBox`](@ref).
"""
function graybox_jvp! end

function add_predictor(model::JuMP.AbstractModel, predictor::GrayBox, x::Vector)
    set = MOI.VectorNonlinearOracle(predictor, length(x))
    y = add_variables(model, x, set.output_dimension, "moai_GrayBox")
    con = JuMP.@constraint(model, [x; y] in set)
    return y, Formulation(predictor, y, [con])
end

function add_predictor(
    ::JuMP.AbstractModel,
    ::ReducedSpace{<:GrayBox},
    ::Vector,
)
    return error("cannot construct reduced-space formulation of GrayBox")
end
